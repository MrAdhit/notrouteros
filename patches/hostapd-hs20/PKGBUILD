_srcname=hostap
_pkgbase=hostapd

pkgname=hostapd-hs20
pkgver=2.11
pkgrel=1
arch=("x86_64")

pkgdesc="hostapd with Hotspot 2.0 and proxy_arp support for NotRouterOS"

url="https://w1.fi/hostapd/"
license=("BSD-3-Clause")

provides=("hostapd")
conflicts=("hostapd")

depends=(
    "glibc"
    "libnl"
    "openssl"
    "sqlite"
)
makedepends=(
    "git"
)

backup=(
    "etc/hostapd/hostapd.accept"
    "etc/hostapd/hostapd.conf"
    "etc/hostapd/hostapd.deny"
    "etc/hostapd/hostapd.eap_user"
    "etc/hostapd/hostapd.radius_clients"
    "etc/hostapd/hostapd.vlan"
    "etc/hostapd/hostapd.wpa_psk"
)

source=(
    "git+https://w1.fi/${_srcname}.git#tag=${_srcname}_${pkgver//./_}"
    "config"
    "hostapd.service"
    "hostapd@.service"
)
sha256sums=(
    "SKIP"
    "b3d3acdc300b0d5463f6a66a5fc672061939645f09471181bd93e0a073e8eda8"
    "989bc6855f44c0b360e3d4cd4a146c35b7c12f8a0ced627b4b033f58edcade8e"
    "926e82166e3848e6932c2fd2b39fa881fbb9f7022695995262a5afd907914e4a"
)

options=(!strip)

prepare() {
    cd "$srcdir/${_srcname}/${_pkgbase}"
    
    # Link our custom build config
    ln -sf "$srcdir/config" .config
}

build() {
    cd "$srcdir/${_srcname}/${_pkgbase}"
    
    make
}

package() {
    cd "$srcdir/${_srcname}"
    
    make -C ${_pkgbase} install DESTDIR="$pkgdir" BINDIR=/usr/bin
    
    install -vDm 644 "$srcdir/hostapd.service" -t "$pkgdir/usr/lib/systemd/system/"
    install -vDm 644 "$srcdir/hostapd@.service" -t "$pkgdir/usr/lib/systemd/system/"
    
    install -vDm 644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
    
    install -vDm 640 ${_pkgbase}/${_pkgbase}.{accept,conf,deny,eap_user,radius_clients,vlan,wpa_psk} -t "$pkgdir/etc/${_pkgbase}/"
    
    install -vDm 644 ${_pkgbase}/{hostapd.sim_db,wired.conf,hlr_auc_gw.{txt,milenage_db}} "${_pkgbase}/"{README*,ChangeLog} -t "$pkgdir/usr/share/doc/$pkgname/"
    
    install -vDm 644 ${_pkgbase}/${_pkgbase}.8 -t "$pkgdir/usr/share/man/man8/"
    install -vDm 644 ${_pkgbase}/${_pkgbase}_cli.1 -t "$pkgdir/usr/share/man/man1/"
}
